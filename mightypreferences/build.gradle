apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

version = '0.0.8'
group = 'com.illuzor.mightypreferences'

ext {
    siteUrl = 'https://github.com/illuzor/Mighty-Preferences'
    gitUrl = 'https://github.com/illuzor/Mighty-Preferences.git'
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 8
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'MightyPreferences'
                url siteUrl
                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/illuzor/Mighty-Preferences/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = project.findProperty("bintrayId")
                        name = project.findProperty("bintrayName")
                        email = project.findProperty("bintrayEmail")
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from 'src/main/java'
}

task classesJar(type: Jar) {
    from "$buildDir/intermediates/classes/release"
}

artifacts {
    archives classesJar
    archives sourcesJar
}

bintray {
    user = project.findProperty("bintrayName")
    key = project.findProperty("bintrayKey")

    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'MightyPreferences'
        desc = 'Kotlin library for andriod. Wrapper for SharedPreferences which provides additional possibilities.'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['MIT']
        publicDownloadNumbers = true
    }
}