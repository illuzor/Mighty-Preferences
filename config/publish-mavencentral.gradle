apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

ext["signing.keyId"] = project.findProperty("signingKeyId")
ext["signing.password"] = project.findProperty("signingPassword")
ext["signing.secretKeyRingFile"] = project.findProperty("signingKeyFile")
ext["ossrhUsername"] = project.findProperty("ossrhUsername")
ext["ossrhPassword"] = project.findProperty("ossrhPassword")
ext["sonatypeStagingProfileId"] = project.findProperty("sonatypeProfileId")

publishing {
    publications {
        release(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION

            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            artifact androidSourcesJar

            pom {
                name = PUBLISH_ARTIFACT_ID
                description = 'Kotlin library for andriod. Wrapper for SharedPreferences which provides additional possibilities'
                url = 'https://github.com/illuzor/Mighty-Preferences'
                licenses {
                    license {
                        name = 'License'
                        url = 'https://github.com/illuzor/Mighty-Preferences/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'illuzor'
                        name = 'Artem Zanin '
                        email = 'illuzor@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:illuzor/Mighty-Preferences.git'
                    developerConnection = 'scm:git@github.com:illuzor/Mighty-Preferences.git'
                    url = 'https://github.com/illuzor/Mighty-Preferences/'
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications
}
